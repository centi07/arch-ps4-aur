# Maintainer: centi_07 <centi07 at proton dot me>
# based xf860video-amdgpu-git by
# Yurii Kolesnykov <root at yurikoles dot com>
#
# Send PRs here: https://github.com/centi07/arch-ps4-aur
#

pkgname=xf86-video-amdgpu-ps4
pkgver=25.0.0
pkgrel=1
pkgdesc="X.org amdgpu video driver (git version)"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('MIT')
depends=('systemd-libs' 'mesa' 'libdrm' 'glibc')
makedepends=('git' 'meson' 'xorg-server-devel' 'systemd')
provides=("xf86-video-amdgpu=${pkgver}")
conflicts=("xf86-video-amdgpu")
groups=('xorg-drivers-git')
source=("${pkgname}::git+https://gitlab.freedesktop.org/xorg/driver/xf86-video-amdgpu.git"
        'ps4-xf86-video-amdgpu.patch')
sha256sums=('SKIP'
            '17f0504fc662a39d48b9bf33fe389472ce369442813d466430a5c54bbfb851e0')

pkgver() {
  cd ${pkgname}
  git describe --tags | sed "s/^xf86-video-amdgpu.//;s/\([^-]*-g\)/r\1/;s/-/./g"
}

prepare() {
  cd "$pkgname"
  patch -Np1 -i "$srcdir/ps4-xf86-video-amdgpu.patch"
}

build() {
  # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
  # With them, module fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/-Wl,-z,now}

  #CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common

  arch-meson "${pkgname}" build -Dglamor=enabled

  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "${pkgdir}" --quiet

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${pkgname}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
